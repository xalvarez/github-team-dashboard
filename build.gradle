plugins {
    id "org.jetbrains.kotlin.jvm" version "$kotlinVersion"
    id "org.jetbrains.kotlin.kapt" version "$kotlinVersion"
    id "org.jetbrains.kotlin.plugin.allopen" version "$kotlinVersion"
    id "io.micronaut.application" version "1.4.2"
    id "com.github.johnrengelman.shadow" version "6.1.0"
    id "org.sonarqube" version "3.1.1"
    id "jacoco"
    id "io.gitlab.arturbosch.detekt" version "1.16.0"
}

repositories {
    mavenCentral()
    jcenter()
}

micronaut {
    runtime "netty"
    testRuntime "junit5"
    processing {
        incremental false
        annotations "com.github.xalvarez.githubteamdashboard.*"
    }
}

dependencies {
    implementation "io.micronaut.kotlin:micronaut-kotlin-runtime"
    implementation "io.micronaut.views:micronaut-views-thymeleaf"
    implementation "io.micronaut:micronaut-http-client"
    implementation "io.micronaut:micronaut-runtime"
    implementation "io.micronaut:micronaut-validation"
    implementation "io.micronaut:micronaut-management"
    implementation "org.jetbrains.kotlin:kotlin-reflect:${kotlinVersion}"

    runtimeOnly "ch.qos.logback:logback-classic"
    runtimeOnly "com.fasterxml.jackson.module:jackson-module-kotlin"
    runtimeOnly "org.webjars:bootstrap:4.6.0"
    runtimeOnly "org.webjars:jquery:3.6.0"

    testImplementation "com.github.tomakehurst:wiremock-jre8:2.27.2"
    testImplementation "org.mockito:mockito-junit-jupiter:3.8.0"
}

application {
    mainClass.set("com.github.xalvarez.githubteamdashboard.ApplicationKt")
}

java {
    sourceCompatibility = JavaVersion.toVersion("15")
}

allOpen {
    annotation("io.micronaut.context.annotation.Prototype")
}

tasks {
    compileKotlin {
        kotlinOptions {
            jvmTarget = "15"
        }
    }
    compileTestKotlin {
        kotlinOptions {
            jvmTarget = "15"
        }
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

project.tasks["sonarqube"].dependsOn "jacocoTestReport"

dockerfile {
    baseImage = "adoptopenjdk/openjdk15:jre-15.0.2_7-alpine"
}

dockerBuild {
    images = [
            "ghcr.io/xalvarez/github-team-dashboard:$project.version"
    ]
}
