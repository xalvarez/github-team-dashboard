import static org.gradle.api.JavaVersion.VERSION_21
import static org.jetbrains.kotlin.gradle.dsl.JvmTarget.JVM_21

buildscript {
    ext {
        kotlinVersion = '1.9.22'
        micronautVersion = '4.3.0'
    }
}

plugins {
    id "org.jetbrains.kotlin.jvm" version "$kotlinVersion"
    id "org.jetbrains.kotlin.plugin.allopen" version "$kotlinVersion"
    id "com.google.devtools.ksp" version "1.9.22-1.0.17"
    id "io.micronaut.application" version "4.3.3"
    id "com.github.johnrengelman.shadow" version "8.1.1"
    id "io.gitlab.arturbosch.detekt" version "1.23.5"
    id "org.jlleitschuh.gradle.ktlint" version "12.1.0"
}

repositories {
    mavenCentral()
}

micronaut {
    runtime "netty"
    testRuntime "junit5"
    processing {
        incremental true
        annotations "com.github.xalvarez.githubteamdashboard.*"
    }
}

dependencies {
    ksp "io.micronaut.serde:micronaut-serde-processor"
    ksp "io.micronaut.validation:micronaut-validation-processor"
    implementation "io.micronaut:micronaut-http-client"
    implementation "io.micronaut.kotlin:micronaut-kotlin-runtime"
    implementation "io.micronaut.reactor:micronaut-reactor"
    implementation "io.micronaut.serde:micronaut-serde-jackson"
    implementation "io.micronaut.validation:micronaut-validation"
    implementation "io.micronaut.views:micronaut-views-thymeleaf"
    implementation "io.micronaut:micronaut-management"
    implementation "io.micronaut:micronaut-runtime"
    implementation "org.jetbrains.kotlin:kotlin-reflect:${kotlinVersion}"

    runtimeOnly "ch.qos.logback:logback-classic"
    runtimeOnly "com.fasterxml.jackson.module:jackson-module-kotlin"
    runtimeOnly "org.webjars:bootstrap:5.3.2"
    runtimeOnly "org.webjars:popper.js:2.11.7"

    testImplementation "org.wiremock:wiremock:3.4.1"
    testImplementation "io.mockk:mockk:1.13.9"
}

application {
    mainClass.set("com.github.xalvarez.githubteamdashboard.ApplicationKt")
}

java.sourceCompatibility(VERSION_21)

allOpen {
    annotation("io.micronaut.context.annotation.Prototype")
}

tasks {
    compileKotlin {
        compilerOptions {
            jvmTarget = JVM_21
        }
    }
    compileTestKotlin {
        compilerOptions {
            jvmTarget = JVM_21
        }
    }
}

detekt {
    config.setFrom(".config/detekt.yml")
    buildUponDefaultConfig = true
}

dockerfile {
    baseImage("eclipse-temurin:21.0.1_12-jre-alpine")
}

dockerBuild {
    images = [
            "ghcr.io/xalvarez/github-team-dashboard:$project.version"
    ]
}
