import static org.gradle.api.JavaVersion.VERSION_21
import static org.jetbrains.kotlin.gradle.dsl.JvmTarget.JVM_21

buildscript {
    ext {
        kotlinVersion = '2.1.20'
        micronautVersion = '4.7.2'
    }
}

plugins {
    id "com.github.johnrengelman.shadow" version "8.1.1"
    id "com.google.devtools.ksp" version "2.1.20-2.0.0"
    id "io.gitlab.arturbosch.detekt" version "1.23.8"
    id "io.micronaut.application" version "4.5.3"
    id "org.jetbrains.kotlin.jvm" version "$kotlinVersion"
    id "org.jetbrains.kotlin.plugin.allopen" version "$kotlinVersion"
    id "org.jlleitschuh.gradle.ktlint" version "12.2.0"
}

repositories {
    mavenCentral()
}

dependencies {
    ksp "io.micronaut.serde:micronaut-serde-processor"
    ksp "io.micronaut.validation:micronaut-validation-processor"
    ksp "io.micronaut:micronaut-http-validation"

    implementation "io.micronaut.kotlin:micronaut-kotlin-runtime"
    implementation "io.micronaut.reactor:micronaut-reactor-http-client"
    implementation "io.micronaut.serde:micronaut-serde-jackson"
    implementation "io.micronaut.validation:micronaut-validation"
    implementation "io.micronaut.views:micronaut-views-thymeleaf"
    implementation "io.micronaut:micronaut-http-client"
    implementation "io.micronaut:micronaut-management"
    implementation "jakarta.validation:jakarta.validation-api"
    implementation "org.jetbrains.kotlin:kotlin-reflect:${kotlinVersion}"

    runtimeOnly "ch.qos.logback:logback-classic"
    runtimeOnly "com.fasterxml.jackson.module:jackson-module-kotlin"
    runtimeOnly "org.webjars:bootstrap:5.3.5"
    runtimeOnly "org.webjars:popper.js:2.11.7"

    testImplementation "io.mockk:mockk:1.14.0"
    testImplementation "org.wiremock:wiremock:3.13.0"
}

micronaut {
    runtime "netty"
    testRuntime "junit5"
    processing {
        incremental true
        annotations "com.github.xalvarez.githubteamdashboard.*"
    }
}

application {
    mainClass.set("com.github.xalvarez.githubteamdashboard.ApplicationKt")
}

java.sourceCompatibility(VERSION_21)

allOpen {
    annotation("io.micronaut.context.annotation.Prototype")
}

tasks {
    compileKotlin {
        compilerOptions {
            jvmTarget = JVM_21
        }
    }
    compileTestKotlin {
        compilerOptions {
            jvmTarget = JVM_21
        }
    }
}

detekt {
    config.setFrom(".config/detekt.yml")
    buildUponDefaultConfig = true
}

dockerfile {
    baseImage("eclipse-temurin:21-jre-alpine")
}

dockerBuild {
    images = [
            "ghcr.io/xalvarez/github-team-dashboard:$project.version"
    ]
}
