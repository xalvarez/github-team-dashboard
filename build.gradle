import static org.gradle.api.JavaVersion.VERSION_17
import static org.jetbrains.kotlin.gradle.dsl.JvmTarget.JVM_17

buildscript {
    ext {
        kotlinVersion = '1.9.10'
        micronautVersion = '4.1.2'
    }
}

plugins {
    id "org.jetbrains.kotlin.jvm" version "$kotlinVersion"
    id "org.jetbrains.kotlin.plugin.allopen" version "$kotlinVersion"
    id "com.google.devtools.ksp" version "1.9.10-1.0.13"
    id "io.micronaut.application" version "$micronautVersion"
    id "com.github.johnrengelman.shadow" version "8.1.1"
    id "io.gitlab.arturbosch.detekt" version "1.23.1"
}

repositories {
    mavenCentral()
}

micronaut {
    runtime "netty"
    testRuntime "junit5"
    processing {
        incremental true
        annotations "com.github.xalvarez.githubteamdashboard.*"
    }
}

dependencies {
    ksp "io.micronaut.serde:micronaut-serde-processor"
    ksp "io.micronaut.validation:micronaut-validation-processor"
    implementation "io.micronaut:micronaut-http-client"
    implementation "io.micronaut.kotlin:micronaut-kotlin-runtime"
    implementation "io.micronaut.serde:micronaut-serde-jackson"
    implementation "io.micronaut.validation:micronaut-validation"
    implementation "io.micronaut.views:micronaut-views-thymeleaf"
    implementation "io.micronaut:micronaut-management"
    implementation "io.micronaut:micronaut-runtime"
    implementation "org.jetbrains.kotlin:kotlin-reflect:${kotlinVersion}"

    runtimeOnly "ch.qos.logback:logback-classic"
    runtimeOnly "com.fasterxml.jackson.module:jackson-module-kotlin"
    runtimeOnly "org.webjars:bootstrap:5.3.2"
    runtimeOnly "org.webjars:popper.js:2.11.7"

    testImplementation "org.wiremock:wiremock:3.2.0"
    testImplementation "io.mockk:mockk:1.13.8"
}

application {
    mainClass.set("com.github.xalvarez.githubteamdashboard.ApplicationKt")
}

java.sourceCompatibility(VERSION_17)

allOpen {
    annotation("io.micronaut.context.annotation.Prototype")
}

tasks {
    compileKotlin {
        compilerOptions {
            jvmTarget = JVM_17
        }
    }
    compileTestKotlin {
        compilerOptions {
            jvmTarget = JVM_17
        }
    }
}

dockerfile {
    baseImage("eclipse-temurin:17.0.8.1_1-jre-alpine")
}

dockerBuild {
    images = [
            "ghcr.io/xalvarez/github-team-dashboard:$project.version"
    ]
}
